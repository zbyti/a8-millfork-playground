const word size = 8192

word RTCLOK @ $13, SAVMSC @ $58
word i@$e0, prime@$e2, k@$e4, count@$e6
pointer screen@$e8

asm void pause() {
  LDA $14
  rt_check:
  CMP $14
  BEQ rt_check
  RTS
}

void printScore() {
  array(byte) tmp[4]
  byte iter

  screen = SAVMSC

  tmp[0] = RTCLOK.lo >> 4
  tmp[1] = RTCLOK.lo & %00001111
  tmp[2] = RTCLOK.hi >> 4
  tmp[3] = RTCLOK.hi & %00001111

  for iter:tmp {
    if tmp[iter] < 10 {
      screen[iter] = tmp[iter] + $10
    } else {
      screen[iter] = tmp[iter] + $17
    }
  }
}

void main() {
  array(byte) flags[size] align(1024)
  byte iter

  pause()
  RTCLOK = 0

  for iter,9,downto,0 {

    count = 0

    for i:flags {
      flags[i] = 1
    }

    for i:flags {
      if flags[i] != 0 {
        prime = (i * 2) + 3
        k = i + prime
        while k <= size {
          flags[k] = 0
          k += prime
        }
        count += 1
      }
    }

  }

  printScore()

  while true {}
}
